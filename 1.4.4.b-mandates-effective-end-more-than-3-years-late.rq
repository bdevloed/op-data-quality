PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
	
PREFIX org: <http://www.w3.org/ns/org#>
PREFIX locn: <http://www.w3.org/ns/locn#>
PREFIX schema: <http://schema.org/>
PREFIX regorg: <http://www.w3.org/ns/regorg#>
PREFIX person: <http://www.w3.org/ns/person#>
PREFIX vcard: <http://www.w3.org/2006/vcard/ns#>
PREFIX dbpedia: <http://dbpedia.org/ontology/>
PREFIX adms: <http://www.w3.org/ns/adms#>
PREFIX euro: <http://data.europa.eu/m8g/>
PREFIX prov: <http://www.w3.org/ns/prov#>
PREFIX organisatie: <https://data.vlaanderen.be/ns/organisatie#>
PREFIX persoon: <https://data.vlaanderen.be/ns/persoon#>
PREFIX adres: <https://data.vlaanderen.be/ns/adres#>
PREFIX generiek: <https://data.vlaanderen.be/ns/generiek#>
PREFIX mandaat: <http://data.vlaanderen.be/ns/mandaat#>
PREFIX besluit: <http://data.vlaanderen.be/ns/besluit#>
PREFIX lblodlg: <https://data.lblod.info/vocabularies/leidinggevenden/>
PREFIX dc_terms: <http://purl.org/dc/terms/>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX euvoc: <http://publications.europa.eu/ontology/euvoc#>
PREFIX ere: <http://data.lblod.info/vocabularies/erediensten/>
PREFIX ch: <http://data.lblod.info/vocabularies/contacthub/>    
PREFIX code: <http://lblod.data.gift/vocabularies/organisatie/>
PREFIX ext: <http://mu.semte.ch/vocabularies/ext/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>

SELECT DISTINCT ?bestuur ?label ?typeEredienst ?provincie ?bestuursOrgaan ?parentBestuursOrgaan ?role ?einde ?mandatarisEinde ?years
FROM <http://mu.semte.ch/graphs/organisatieportaal>
WHERE {
  ?bestuur
    a
      ?type;
    regorg:orgStatus/skos:prefLabel
      "Actief".

  ?bestuur ^besluit:bestuurt ?parentBestuursOrgaan.
  ?bestuursOrgaan generiek:isTijdspecialisatieVan ?parentBestuursOrgaan.

  ?bestuursOrgaan
    org:hasPost
      ?post.
  ?post
    org:role/skos:prefLabel
      ?role.

  ?mandataris
    	org:holds ?post;
   		mandaat:einde ?mandatarisEinde.
  
  ?bestuursOrgaan mandaat:bindingEinde ?einde.

  FILTER(?einde > now())
  FILTER(?mandatarisEinde != "1900-01-01"^^xsd:date)
  
  BIND( 
  year(?einde) - year(?mandatarisEinde) 
  - if(
      month(?einde) < month(?mandatarisEinde) || 
      ( 
        month(?einde) = month(?mandatarisEinde) &&
        day(?einde) < day(?mandatarisEinde)
      )
    ,1,0 ) 
  as ?years )
  
  FILTER (?years > 3)

  OPTIONAL {
    ?bestuur
      skos:prefLabel
        ?label
  }
  OPTIONAL {
  	?bestuur
    	ere:typeEredienst
      		?typeEredienstUri.
    OPTIONAL {
    	?typeEredienstUri
        	skos:prefLabel
          	?typeEredienst.
	  }
  }
  OPTIONAL {
    ?bestuur
      org:hasPrimarySite
        ?primarySite.
    OPTIONAL {    
      ?primarySite
        organisatie:bestaatUit
          ?sa.
	    OPTIONAL {
        ?sa
          locn:adminUnitL2
            ?provincie.
      }
    }
  }

  FILTER (?type IN (
    <http://data.lblod.info/vocabularies/erediensten/CentraalBestuurVanDeEredienst>,
    <http://data.lblod.info/vocabularies/erediensten/BestuurVanDeEredienst>
  ))
}