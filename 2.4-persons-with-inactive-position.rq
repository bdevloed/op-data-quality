PREFIX person: <http://www.w3.org/ns/person#>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX org: <http://www.w3.org/ns/org#>
PREFIX mandaat: <http://data.vlaanderen.be/ns/mandaat#>
PREFIX ch: <http://data.lblod.info/vocabularies/contacthub/>
PREFIX ere: <http://data.lblod.info/vocabularies/erediensten/>

SELECT DISTINCT ?person ?firstName ?lastName
FROM <http://mu.semte.ch/graphs/organisatieportaal>
WHERE {
  ?person a person:Person ;
    foaf:givenName ?firstName ;
    foaf:familyName ?lastName ;
    (^org:heldBy)|(^mandaat:isBestuurlijkeAliasVan) ?position .

  FILTER NOT EXISTS {
    {
      ?person (^org:heldBy/ch:eindedatum)|(^mandaat:isBestuurlijkeAliasVan/mandaat:einde) ?endPosition .
      FILTER (NOW() < ?endPosition)
    }
    UNION
    {
      ?person (^org:heldBy)|(^mandaat:isBestuurlijkeAliasVan) ?position.
      FILTER NOT EXISTS { ?position ch:eindedatum|mandaat:einde ?endPosition . }
    }
  }
}
